-- Class of Match manager
class MatchManager is subclass of Environment, MyPrintTestListener


instance variables

-- matchmakes
matchmakes : map MatchID to MatchMake := {|->};
inv 
  (forall id in set dom matchmakes & id = matchmakes(id).getID())
    and
  (forall m in set rng matchmakes & valid(m))
    and
  (forall mm1, mm2 in set rng matchmakes & 
    mm1 <> mm2 => consistent(mm1, mm2));



functions

-- dupulication check
public consistent : MatchMake * MatchMake -> bool
consistent(mm1, mm2) ==
   mm1.getID() <> mm2.getID() or equals(mm1.getMatch(), mm2.getMatch());


operations

-- validation check
public pure valid : MatchMake ==> bool
valid(mm) ==
  return
    mm.getID() in set getAllMatchIDs();

-- add MatchMake
public addMatchMake : MatchMake ==> ()
addMatchMake(matchmake) ==
  atomic(
    matchmakes := matchmakes munion {matchmake.getID() |-> matchmake};
  )
pre 
  matchmake.getID() not in set dom matchmakes
    and
  valid(matchmake)
    and
  forall n in set rng matchmakes & consistent(matchmake, n)
post matchmake in set rng matchmakes;


-- get MatchMake from specific ID
public pure getMatchMake : MatchID ==> MatchMake
getMatchMake(matchID) ==
  return matchmakes(matchID)
pre matchID in set dom matchmakes;



end MatchManager
